buildscript {
    ext {
        springBootVersion = '1.5.2.RELEASE'
        verifier_version = "1.0.4.RELEASE"

    }
    repositories {
        mavenCentral()
        maven { url "http://repo.spring.io/release" }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath "org.springframework.cloud:spring-cloud-contract-gradle-plugin:${verifier_version}"
    }
}


apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'groovy'
apply plugin: 'spring-cloud-contract'
apply plugin: 'eclipse'
apply plugin: "idea"


jar {
    baseName = 'sample-app'
    version = ''
}

repositories {
    mavenCentral()
    maven { url "https://oss.sonatype.org/content/repositories/releases/" }
    maven { url "https://plugins.gradle.org/m2/" }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8


// Ensure that the contracts are regenerated after clean build.
clean {
    delete 'src/test/java/generatedContractTests'
}


dependencies {
    compile("org.springframework.boot:spring-boot-starter-web") {
        exclude module: "spring-boot-starter-tomcat"
    }

    compile("org.springframework.boot:spring-boot-starter-jetty")
    compile("org.springframework.boot:spring-boot-starter-actuator")


    //Test
    testCompile("org.springframework.boot:spring-boot-starter-test")

    //Cloud Contract Testing
    testCompile 'org.codehaus.groovy:groovy-all:2.4.6'
    testCompile group: 'org.springframework.cloud', name: 'spring-cloud-starter-contract-verifier', version: '1.0.1.RELEASE'


}

/*
 Configure Contract Tests. Generate them into a directory in our test sourceset instead of the build dir to
 ease debugging.

*/
contracts {
    basePackageForTests = 'de.sample.app'
    baseClassForTests = 'de.sample.app.contacts.util.ContractTesting'
    generatedTestSourcesDir = project.file('src/test/java/generatedContractTests')
    targetFramework = 'JUnit'
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.1'
}
